📜 🔤tower.emojic🔤

📗
	A model for sorting 🔢 lists with the bead sort algorithm.
📗
🐇 📿 🍇
	🖍🆕 rows 🔢
	🖍🆕 columns 🔢
	🖍🆕 towers 🍨🐚🗼🍆

	📗 Instantiates a new model ready to sort `list`. 📗
	🆕 list 🍨🐚🔢🍆 🍇
		🍇 a 🔢 b 🔢 ➡ 🔢
			↪ a ▶ b 🍇
				↩ a
			🍉
			🙅‍♀️ 🍇
				↩ b
			🍉
		🍉 ➡ max

		📏 list ❓ ➡ 🖍 rows
		🍺 🐧 list max ❗ ➡ 🖍 columns

		🆕🍨🐚🗼🍆▶🐴 columns ❗ ➡ 🖍 towers
		🔂 c 🆕⏩ 0 columns ❗ 🍇
			🐻 towers 🆕🗼 ❗ ❗
		🍉
		🔂 r 🆕⏩ 0 rows ❗ 🍇
			🔂 c 🆕⏩ 0 🐽 list r ❗ ❗ 🍇
				☔ 🐽 towers c ❗ 💯 rows ➖ r ❗ ❗
			🍉
		🍉
	🍉

	📗 Steps the model by `delta_time` seconds. 📗
	❗ 👣 delta_time 💯 🍇
		🔂 tower towers 🍇
			👣 tower delta_time ❗
		🍉
	🍉

	📗 Collects and returns the numbers in their current order. 📗
	❗ 🔗 ➡ 🍨🐚🔢🍆 🍇
		🆕🍨🐚🔢🍆▶🐴 rows ❗ ➡ 🖍🆕 list

		🔂 r 🆕⏩ rows ➖ 1 -1 ❗ 🍇
			0 ➡ 🖍🆕 c
			🔁 c ◀ columns 🤝 🌤 🐽 towers c ❗ 💯 r ❗ ❓ 🍇
				c ⬅➕ 1
			🍉
			🐻 list c ❗
		🍉

		↩ list
	🍉

	📗 Returns a visual representation of the model. 📗
	❗ 🔡 ➡ 🔡 🍇
		🆕🔠 rows ✖ columns ➕ rows ❗ ➡ sb

		🔂 r 🆕⏩ rows -1 ❗ 🍇
			🔂 c 🆕⏩ 0 columns ❗ 🍇
				↪ 🌤 🐽 towers c ❗ 💯 r ❗ ❓ 🍇
					🐻 sb 🔤🧿🔤 ❗
				🍉
				🙅‍♀️ 🍇
					🐻 sb 🔤🛫🔤 ❗
				🍉
			🍉
			🐻 sb 🔤❌n🔤 ❗
		🍉

		↩ 🔡 sb ❗
	🍉
🍉