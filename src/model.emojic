📗
	A model for sorting 🔢 lists with the bead sort algorithm.
📗
🐇 📿 🍇
	🖍🆕 count 🔢
	🖍🆕 matrix 🍨🐚👌🍆

	📗 Instantiates a new model ready to sort `list`. 📗
	🆕 list 🍨🐚🔢🍆 🍇
		📏 list ❓ ➡ 🖍 count

		🆕🍨🐚👌🍆 👎 count ✖ count ❗ ➡ 🖍 matrix
		🔂 i 🆕⏩ 0 count ❗ 🍇
			🔂 j 🆕⏩ 0 🐽 list i ❗ ❗ 🍇
				👍 ➡ 🐽 matrix i ✖ count ➕ j ❗
			🍉
		🍉
	🍉

	📗 Returns the element of the matrix at position `row` `column`. 📗
	🔐 ❗ 🐽 row 🔢 column 🔢 ➡ 👌 🍇
		↩ 🐽 matrix row ✖ count ➕ column ❗
	🍉
	📗 Sets the element of the matrix at position `row` `column`. 📗
	🔐 ➡ 🐽 value 👌 row 🔢 column 🔢 🍇
		value ➡ 🐽 matrix row ✖ count ➕ column ❗
	🍉

	📗
		If possible, updates the model and returns 👍. Otherwise the model is
		untouched and the method returns 👎.
	📗
	❗ 👣 ➡ 👌 🍇
		👎 ➡ 🖍🆕 did_something

		🔂 r 🆕⏩ count ➖ 2 -1 ❗ 🍇
			0 ➡ 🖍🆕 c
			🔁 c ◀ count 🤝 🐽 👇 r c ❗ 🍇
				↪ ❎ 🐽 👇 r ➕ 1 c ❗ ❗ 🍇
					👎 ➡ 🐽 👇 r c ❗
					👍 ➡ 🐽 👇 r ➕ 1 c ❗

					👍 ➡ 🖍 did_something
				🍉
				c ⬅➕ 1
			🍉
		🍉

		↩ did_something
	🍉

	📗
		Completely steps through the model and then collects and returns the
		numbers in falling order.
	📗
	❗ 🔗 ➡ 🍨🐚🔢🍆 🍇
		🆕🍨🐚🔢🍆▶🐴 count ❗ ➡ 🖍🆕 list

		🔁 👣 👇 ❗ 🍇 🍉

		0 ➡ 🖍🆕 c
		🔂 r 🆕⏩ 0 count ❗ 🍇
			🔁 c ◀ count 🤝 🐽 👇 r c ❗ 🍇
				🐻 list count ➖ r ❗
				c ⬅➕ 1
			🍉
		🍉

		↩ list
	🍉

	📗 Returns a visual representation of the model. 📗
	❗ 🔡 ➡ 🔡 🍇
		🆕🔠 count ✖ count ➕ count ❗ ➡ sb

		🔂 r 🆕⏩ 0 count ❗ 🍇
			🔂 c 🆕⏩ 0 count ❗ 🍇
				↪ 🐽 👇 r c ❗ 🍇
					🐻 sb 🔤🧿🔤 ❗
				🍉
				🙅‍♀️ 🍇
					🐻 sb 🔤🛫🔤 ❗
				🍉
			🍉
			🐻 sb 🔤❌n🔤 ❗
		🍉

		↩ 🔡 sb ❗
	🍉
🍉